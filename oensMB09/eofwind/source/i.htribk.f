      SUBROUTINE HTRIBK(NM,N,AR,AI,D,M,ZR,ZI)
C*
C*    BACK TRANSFORM THE EIGENVECTORS OF THAT SYMMETRIC
C*    TRIDIAGONAL MATRIX DETERMINED BY HTRIDI
C*
      INTEGER NM,N,M
      REAL AR(NM,N),AI(NM,N),D(2,N),ZR(NM,M),ZI(NM,M)
C*
      INTEGER I,J,K,L
      REAL H,SR,SI
C*
      IF (M .EQ. 0) RETURN
C*
C*    TRANSFORM THE EIGENVECTORS OF THE REAL SYMMETRIC
C*    TRIDIAGONAL MATRIX TO THOSE OF THE HERMITIAN
C*    TRIDIAGONAL MATRIX
C*
      DO K=1,N
      DO J=1,M
      ZI(K,J)=-ZR(K,J)*D(2,K)
      ZR(K,J)= ZR(K,J)*D(1,K)
      ENDDO
      ENDDO
C*
      IF (N .EQ. 1) RETURN
C*
C*    RECOVER AN APPLY THE HOUSEHOUDER MATRICES
C*
      DO I=2,N
C*
      L=I-1
      H=AI(I,I)
C*
      IF (H .NE. 0.0) THEN
C*
      DO J=1,M
C*
      SR=0.0
      SI=0.0
      DO K=1,L
      SR=SR+AR(I,K)*ZR(K,J)-AI(I,K)*ZI(K,J)
      SI=SI+AR(I,K)*ZI(K,J)+AI(I,K)*ZR(K,J)
      ENDDO
C*
C*    DOUBLE DIVISION AVOID POSSIBLE UNDERFLOW
C*
      SR=(SR/H)/H
      SI=(SI/H)/H
C*
      DO K=1,L
      ZR(K,J)=ZR(K,J)-SR*AR(I,K)-SI*AI(I,K)
      ZI(K,J)=ZI(K,J)-SI*AR(I,K)+SR*AI(I,K)
      ENDDO
C*
      ENDDO
C*
      ENDIF
C*
      ENDDO
C*
      RETURN
      END
