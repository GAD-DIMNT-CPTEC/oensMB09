      SUBROUTINE FFTPLN(GLAT)
C*
#include <fftpln.h>
C*
      INTEGER IMX,IMXT,JMAXHF
      PARAMETER (IMX=IMAX+2,IMXT=3*IMAX/2+1,JMAXHF=JMAX/2)
      INTEGER MEND1,MEND2,MEND3,MNWV2,MNWV0,MNWV3,MNWV1
      PARAMETER (MEND1=MEND+1,MEND2=MEND+2,MEND3=MEND+3,
     *           MNWV2=MEND1*MEND2,MNWV0=MNWV2/2,
     *           MNWV3=MNWV2+2*MEND1,MNWV1=MNWV3/2)
C*
      INTEGER J,L,NN,MMAX,MM,LX,K,I,MN
      REAL RD
C*
      REAL PLN(MNWV1),WGT(JMAXHF),GLAT(JMAX),
     *     RCS2(JMAXHF),DER(MNWV0),PLNWCS(MNWV0),
     *     COLRAD(JMAXHF)
C*
      INTEGER LA0(MEND1,MEND1),LA1(MEND1,MEND2)
      INTEGER IFAX(13)
      REAL TRIGS(IMXT)
      REAL EPS(MNWV1)
      REAL QLN(MNWV2,JMAXHF),QLNV(MNWV3,JMAXHF)
      REAL QLNW(MNWV2,JMAXHF),QDER(MNWV2,JMAXHF),
     *     QLNWCS(MNWV2,JMAXHF)
      REAL AN(IMX,KMAX),AS(IMX,KMAX),BN(IMX,KMAX),
     *     BS(IMX,KMAX),CW(IMX,KMAX)
C*
      COMMON /LA0LA1/ LA0,LA1
      COMMON /FFTCMN/ TRIGS,IFAX
      COMMON /PLNEPS/ EPS
      COMMON /PLNCMN/ QLN,QLNV
      COMMON /PLNDRV/ QLNW,QDER,QLNWCS
      COMMON /WKAREA/ AN,AS,BN,BS,CW
C*
C*    SET FFT
C*
      CALL FAX(IFAX,IMAX,2)
      CALL FFTRIG(TRIGS,IMAX,2)
C*
C*    SET PLN
C*
      L=0
      DO NN=1,MEND1
      MMAX=MEND2-NN
      DO MM=1,MMAX
      L=L+1
      LA0(MM,NN)=L
      ENDDO
      ENDDO
C*
      L=0
      DO MM=1,MEND1
      L=L+1
      LA1(MM,1)=L
      ENDDO
      DO NN=2,MEND2
      MMAX=MEND3-NN
      DO MM=1,MMAX
      L=L+1
      LA1(MM,NN)=L
      ENDDO
      ENDDO
C*
      CALL EPSLON(EPS)
      CALL GLATS(COLRAD,WGT,RCS2)
C*
      RD=45.0/ATAN(1.0)
C*
      DO J=1,JMAXHF
C*
      GLAT(J)=90.0-COLRAD(J)*RD
      GLAT(JMAX-J+1)=-GLAT(J)
C*
      CALL PLN2(PLN,COLRAD,J,EPS,LA1)
      L=0
      DO NN=1,MEND1
      MMAX=MEND2-NN
      DO MM=1,MMAX
      L=L+1
      LX=LA1(MM,NN)
      QLN(2*L-1,J)=PLN(LX)
      QLN(2*L  ,J)=PLN(LX)
      ENDDO
      ENDDO
      DO MN=1,MNWV2
      QLNW(MN,J)=QLN(MN,J)*WGT(J)
      ENDDO
      DO MN=1,MNWV1
      QLNV(2*MN-1,J)=PLN(MN)
      QLNV(2*MN  ,J)=PLN(MN)
      ENDDO
C*
      CALL PLNDER(PLN,DER,PLNWCS,RCS2(J),WGT(J),EPS,LA1)
      DO MN=1,MNWV0
      QDER  (2*MN-1,J)=DER   (MN)
      QDER  (2*MN  ,J)=DER   (MN)
      QLNWCS(2*MN-1,J)=PLNWCS(MN)
      QLNWCS(2*MN  ,J)=PLNWCS(MN)
      ENDDO
C*
      ENDDO
C*
      RETURN
      END
