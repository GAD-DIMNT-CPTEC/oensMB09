      SUBROUTINE FFTPLN
C*
      INCLUDE "reshsst.h"
C*
      INTEGER IMX,IMXT,JMAXHF
      PARAMETER (IMX=IMAX+2,IMXT=IMAX*3/2+1,JMAXHF=(JMAX+1)/2)
      INTEGER MEND,MEND2,MNWV2,MNWV3,MNWV1
      PARAMETER (MEND=MEND1-1,MEND2=MEND1+1,MNWV2=MEND1*MEND2,
     *           MNWV3=MNWV2+2*MEND1,MNWV1=MNWV3/2)
C*
      REAL PLN(MNWV1),DEPS(MNWV1),WGT(JMAXHF),
     *     WGTCS(JMAXHF),DRCS2(JMAXHF)
C*
      INTEGER IFAX(13)
      REAL FN(IMX),FS(IMX),GN(IMX),GS(IMX),TRIGS(IMXT)
      INTEGER LA0(MEND1,MEND1),LA1(MEND1,MEND2)
      REAL QLN(MNWV3,JMAXHF),QLNW(MNWV3,JMAXHF),
     *     DCOLRA(JMAXHF),QW(MNWV2,2),GW(IMX)
C*
      COMMON /FFTCMN/ FN,FS,GN,GS,TRIGS,IFAX
      COMMON /PLNCMN/ QLN,DCOLRA,QW,GW,QLNW,LA0,LA1
C*
      INTEGER J,L,NN,MMAX,MM,LX
C*
C*    SET FFT
C*
      CALL FAX   (IFAX ,IMAX,2)
      CALL FFTRIG(TRIGS,IMAX,2)
C*
C*    SET PLN
C*
      L=0
      DO NN=1,MEND1
      MMAX=MEND1-NN+1
      DO MM=1,MMAX
      L=L+1
      LA0(MM,NN)=L
      ENDDO
      ENDDO
C*
      L=0
      DO NN=1,MEND2
      IF(NN.LE.2) THEN
      MMAX=MEND1
      ELSE
      MMAX=MEND2-NN+1
      ENDIF
      DO MM=1,MMAX
      L=L+1
      LA1(MM,NN)=L
      ENDDO
      ENDDO
C*
      CALL EPSLON(DEPS,MEND1,MEND2,MEND2,MNWV1)
      CALL GLATS(JMAXHF,DCOLRA,WGT,WGTCS,DRCS2)
C*
      DO J=1,JMAXHF
      CALL PLN2(PLN,DCOLRA,J,DEPS,MEND1,MEND2,MEND2,MNWV1,JMAXHF,LA1)
      L=0
      DO NN=1,MEND1
      MMAX=MEND1-NN+1
      DO MM=1,MMAX
      L=L+1
      LX=LA1(MM,NN)
      QLN(2*L-1,J)=PLN(LX)
      QLN(2*L  ,J)=PLN(LX)
      ENDDO
      ENDDO
      DO MM=1,MNWV2
      QLNW(MM,J)=QLN(MM,J)*WGT(J)
      ENDDO
      ENDDO
C*
      RETURN
      END
