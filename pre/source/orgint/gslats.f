      SUBROUTINE GSLATS(KHALF,COLRAD,WGT)
C*
C*    CALCULATES GAUSSIAN LATITUDES AND GAUSSIAN WEIGHTS.
C*    FOR USE IN GRID-SPECTRAL AND SPECTRAL-GRID TRANSFORMS.
C*
C*    ARGUMENT(DIMENSIONS)                       DESCRIPTION
C
C           KHALF                 INPUT : NUMBER OF GAUSSIAN LATITUDES
C                                         IN ONE HEMISPHERE. SET IN MAIN
C                                         ROUTINE.
C         COLRAD(KHALF)          OUTPUT : CO-LATITUDES FOR GAUSSIAN
C                                         LATITUDES IN ONE HEMISPHERE.
C          WGT(KHALF)            OUTPUT : GAUSSIAN WEIGHTS.
C*
C*    GLATS IS CALLED BY THE MAIN ROUTINE.
C*    GLATS CALLS THE FOLLOWING SUBROUTINE: POLY.
C*    GLATS DOES NOT REFER TO ANY COMMONS.
C*
      INTEGER KHALF
      DOUBLE PRECISION COLRAD(KHALF),WGT(KHALF)
C*
      INTEGER K2,K1,K
      DOUBLE PRECISION EPS,SCALE,PI,DRADZ,DRAD,RAD,P1,P2,PHI,X
      DOUBLE PRECISION ZERO,ONE,TWO,FOUR,THSXHD,PT25
      DATA ZERO /0.0D0/, ONE /1.0D0/, TWO /2.0D0/, FOUR /4.0D0/
      DATA THSXHD /360.0D0/, PT25 /0.25D0/, EPS /1.0D-10/
C*
      K2=2*KHALF
      SCALE=TWO/DBLE(K2*K2)
      K1=K2-1
      PI=DATAN(ONE)*FOUR
      DRADZ=PI/THSXHD
      RAD=ZERO
C*
      DO 10 K=1,KHALF
      DRAD=DRADZ
C*
   20 CALL POLY(K2,RAD,P2)
C*
   30 P1=P2
      RAD=RAD+DRAD
      CALL POLY(K2,RAD,P2)
      IF (SIGN(ONE,P1) .EQ. SIGN(ONE,P2)) GOTO 30
C*
      IF (DRAD .LT. EPS) GOTO 40
      RAD=RAD-DRAD
      DRAD=DRAD*PT25
      GOTO 20
C*
   40 CONTINUE
      COLRAD(K)=RAD
      CALL POLY(K1,RAD,P1)
      X=COS(RAD)
      WGT(K)=SCALE*(ONE-X*X)/(P1*P1)
      CALL POLY(K2,RAD,P1)
C*
   10 CONTINUE
C*
      WRITE(*,50)
   50 FORMAT ('  K   COLAT   COLRAD     WGT')
C*
      RAD=THSXHD/(TWO*PI)
      DO K=1,KHALF
      PHI=COLRAD(K)*RAD
      WRITE(*,60) K,PHI,COLRAD(K),WGT(K)
      ENDDO
   60 FORMAT(1X,I2,2X,F6.2,2X,F10.7,2X,E13.7)
C*
      WRITE(*,70) KHALF
   70 FORMAT(' SHALOM FROM GLATS FOR ',I3)
C*
      RETURN
      END
