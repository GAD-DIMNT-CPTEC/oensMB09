      SUBROUTINE EPSLON(EPS,MEND1,NEND2,JEND2,MNWV1)
C*
C*    CALCULATES AN ARRAY WHICH IS USED IN A RECURSION RELATION
C*    TO CALCULATE THE ASSOCIATED LEGENDRE FUNCTIONS.
C*    THIS ARRAY IS ALSO USED TO CALCULATE SINE WEIGHTED
C*    ASSOCIATED LEGENDRE FUNCTIONS AND COSINE WEIGHTED
C*    DERIVATIVES OF ASSOCIATED LEGENDRE FUNCTIONS.
C*
C*    ARGUMENT(DIMENSIONS)                       DESCRIPTION
C*
C*        EPS(MNWV1)             OUTPUT : ARRAY USED IN VARIOUS
C*                                        CALCULATIONS INVOLVING
C*                                        SPHERICAL HARMONICS.
C*                                        VALUES AT THE END OF EACH
C*                                        COLUMN ARE STORED FROM MNWV0+1
C*           MNWV1                INPUT : NUMBER OF WAVE COEFFICIENTS
C*           MEND1                INPUT : TRUNCATION OF THE RHOMBOIDAL
C*           NEND2                        SPECTRAL DOMAIN. SET IN MAIN
C*           JEND2                        ROUTINE.
C*
C*    EPSLON IS CALLED BY THE MAIN ROUTINE.
C*    EPSLON CALLS NO SUBROUTINES.
C*    EPSLON DOES NOT REFER TO ANY COMMONS.
C*
      INTEGER MEND1,NEND2,JEND2,MNWV1
      DOUBLE PRECISION EPS(MNWV1)
C*
      INTEGER L,NN,MMAX,MM
      DOUBLE PRECISION ZERO,ONE,FOUR,AM,AN
      DATA ZERO /0.0D0/, ONE /1.0D0/, FOUR /4.0D0/
C*
      DO L=1, MEND1
      EPS(L)=ZERO
      ENDDO
C*
      L=MEND1
      DO NN=2,NEND2
      IF(NN.LE.JEND2-MEND1+1) THEN
      MMAX=MEND1
      ELSE
      MMAX=JEND2-NN+1
      END IF
      DO MM=1,MMAX
      L=L+1
      AM=DBLE(MM-1)
      AN=DBLE(MM+NN-2)
      EPS(L)=SQRT((AN*AN-AM*AM)/(FOUR*AN*AN-ONE))
      ENDDO
      ENDDO
C*
      RETURN
      END
