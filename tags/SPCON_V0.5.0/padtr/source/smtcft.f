      SUBROUTINE SMTCFT(CFT,MEND1,MNWV2,KDIM)
C*
C*    SMOOTHES SPHERICAL HARMONICS COEFFICIENTS
C*                       USING HOSKIN'S(?) FILTER
C*
C*    INPUT:
C*    CFT(MNWV2,KDIM) - SPHERICAL HARMONICS COEFFICIENTS
C*    MEND1           - TRIANGULAR TRUNCATION (PLUS ONE)
C*    MNWV2           - NUMBER OF COEFFICIENTS (=2*MEND1*(MEND1+1))
C*    KDIM            - NUMBER OF VERTICAL LAYERS
C*
C*    OUTPUT:
C*    CFT(MNWV2) - SMOOTHED SPHERICAL HARMONICS COEFFICIENTS
C*
      INTEGER MEND1,MNWV2,KDIM
      REAL CFT(MNWV2,KDIM)
C*
      INTEGER MN,NN,MM,MMAX,MNS,K
      REAL CS,RM,CX,RMN,CK
C*
C*    DEFINING CONSTANTS
C*
C*    CS - COEFFICIENT FOR FILTERING:
C*       . THE COEFFICIENTS FOR MEND1 WILL BE SMOOTHED
C*         BY A FACTOR CK OF 0.85.
C*       . THE FIRST COEFFICIENT WILL NOT BE SMOOTHED
C*         THE FACTOR CK IS 1.0.
C*
      CS=1.0/0.85
      RM=FLOAT(MEND1-1)*FLOAT(MEND1)
      CX=-LOG(CS)/(RM*RM)
C*
C*    BEGIN SMOOTHING
C*
      MN=0
      DO NN=1,MEND1
C*
      MMAX=MEND1-NN+1
C*
      DO MM=1,MMAX
      MN=MN+1
C*
C*    PERFORMING SMOOTHING
C*
      MNS=MM+NN-1
      RMN=FLOAT(MNS-1)*FLOAT(MNS)
      CK=EXP(CX*RMN*RMN)
C*
      DO K=1,KDIM
      CFT(2*MN-1,K)=CK*CFT(2*MN-1,K)
      CFT(2*MN  ,K)=CK*CFT(2*MN  ,K)
      ENDDO
C*
      ENDDO
C*
      ENDDO
C*
      RETURN
      END
