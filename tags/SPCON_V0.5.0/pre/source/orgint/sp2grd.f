      SUBROUTINE SP2GRD(CFT,GRD)
C*
C*    GENERAL PURPOSE TRANSFORMATION TO GLOBAL GRID POINTS DATA
C*                    FROM SPHERICAL HARMONICS COEFFICIENTS
C*
C*    INPUT:
C*    CFT(MNWV2)     - SPHERICAL HARMONIC COEFFICIENTS
C*
C*    OUTPUT:
C*    GRD(IMAX,JMAX) - GLOBAL GRID POINTS DATA
C*
      INTEGER MEND1,NEND1,JEND1,IMAX,JMAX
C*
      INCLUDE "reshoro.h"
C
      INTEGER IMX,JMAXHF,MNWV2,MNWV3,MNWV1,MNWV0,NEND2,JEND2
      PARAMETER(IMX=IMAX+2,JMAXHF=(JMAX+1)/2,
     2          MNWV2=JEND1*(JEND1+1)
     2                -(JEND1-NEND1)*(JEND1-NEND1+1)
     3                -(JEND1-MEND1)*(JEND1-MEND1+1),
     4          MNWV3=MNWV2+2*MEND1,MNWV1=MNWV3/2,
     6          MNWV0=MNWV2/2,NEND2=NEND1+1,JEND2=JEND1+1)
C*
      REAL CFT(MNWV2),GRD(IMAX,JMAX)
C*
      DOUBLE PRECISION PLN,DEPS,DCOLRA,WGT
      REAL QLN
      COMMON /PLNCOM/ PLN(MNWV1),DEPS(MNWV1),
     *                DCOLRA(JMAXHF),WGT(JMAXHF),
     *                QLN(MNWV2)
C*
      REAL TRIGS
      INTEGER IFAX,LA0,LA1
      COMMON /COMBIT/ TRIGS(IMAX*3/2+1),IFAX(13),
     *                LA0(MEND1,NEND1),LA1(MEND1,NEND2)
C*
      REAL QWORK,GWORK,GN,GS
      COMMON /SCRTCH/ QWORK(MNWV2,2),GWORK(IMX),GN(IMX),GS(IMX)
C*
      INTEGER LAT,L,NN,MMAX,MM,LX,MN
C*
C*    LATITUDE LOOP
C*
      DO LAT=1,JMAXHF
C*
C*    COMPUTES LEGENDRE ASSOCIATED FUNCTIONS (LAF)
C*
      CALL PLN2(PLN,DCOLRA,LAT,DEPS,MEND1,NEND2,JEND2,MNWV1,JMAXHF,LA1)
C*
      IF(JEND1.NE.NEND1+MEND1-1) THEN
      L=0
      DO NN=1,NEND1
      IF(NN.LE.JEND1-MEND1+1) THEN
      MMAX=MEND1
      ELSE
      MMAX=JEND1-NN+1
      END IF
      DO MM=1,MMAX
      L=L+1
      LX=LA1(MM,NN)
      QLN(2*L-1)=PLN(LX)
      QLN(2*L  )=PLN(LX)
      ENDDO
      ENDDO
      ELSE
      DO MN=1,MNWV0
      QLN(2*MN-1)=PLN(MN)
      QLN(2*MN  )=PLN(MN)
      ENDDO
      ENDIF
C*
C*    RESET FOURIER COEFFICIENTS TO ZERO
C*
      CALL RESET(GN,IMX)
      CALL RESET(GS,IMX)
C*
C*    COMPUTES FOURIER COEFFICIENTS
C*
      CALL SUMPLS(CFT,GN,GS,QLN,IMX,MEND1,NEND1,JEND1,MNWV2,1,QWORK)
C*
C*    COMPUTES GRID POINT VALUES
C*
      CALL STOGRD(GN,GRD(1,LAT),1,IMX,IMAX,1,IFAX,TRIGS,GWORK)
      CALL STOGRD(GS,GRD(1,JMAX+1-LAT),1,IMX,IMAX,1,IFAX,TRIGS,GWORK)
C*
      ENDDO
C*
      RETURN
      END
