      SUBROUTINE VTERP(FIELDA,LONIN,LATIN,FIELDB,LONOUT,LATOUT,
     *WTLON,WTLAT,MPLON,MPLAT,LOND,LATD,MSKIN,UNDEF,WORK,NCAT,WRK2,
     *KLASS)
      INTEGER FIELDA,FIELDB,UNDEF
      DIMENSION WTLON(LONIN+LONOUT+2),WTLAT(LATIN+LATOUT+2),
     *MPLON(LONIN+LONOUT+2,2),MPLAT(LATIN+LATOUT+2,2),
     *FIELDA(LONIN,LATIN),FIELDB(LONOUT,LATOUT),WRK2(LONOUT,LATOUT),
     *MSKIN(LONIN,LATIN),WORK(NCAT,LONOUT,LATOUT),KLASS(NCAT),
     *MDIST(7),NDIST(7),B(5)
C     NEW INTEGER FIELD RANKING BY AREA INTERPOLATION SUBROUTINE
C     SHOULD ONLY BE CALLED AFTER SUBROUTINE WTERP HAS BEEN CALLED
C
C     SUBROUTINE ARGUMENTS:
C
C     FIELDA INPUT FIELD TO BE RANK INTERPOLATED
C     LONIN  NUMBER OF LONGITUDE POINTS FOR THE INPUT GRID
C     LATIN  NUMBER OF LATITUDE POINTS FOR THE INPUT GRID
C     FIELDB OUTPUT FIELD RESULTING FROM RANK INTERPOLATION
C     LONOUT NUMBER OF LONGITUDE POINTS FOR THE OUTPUT GRID
C     LATOUT NUMBER OF LATITUDE POINTS FOR THE OUTPUT GRID
C     WTLON  AREA WEIGHTS IN THE LONGITUDINAL DIRECTION
C     WTLAT  AREA WEIGHTS IN THE LATITUDIANL DIRECTION
C     MPLON  LONGITUDE INDEX MAPPING FROM INPUT (,1) TO OUTPUT (,2)
C     MPLAT  LATITUDE INDEX MAPPING FROM INPUT (,1) TO OUTPUT (,2)
C     LOND   TOTAL NUMBER OF LONGITUDE WEIGHTS
C     LATD   TOTAL NUMBER OF LATITUDE WEIGTHS
C     MSKIN  INPUT GRID MASK TO CONFINE INTERPOLATION OF INPUT
C            DATA TO CERTAIN AREAS (1=INTERPOLATE, 0=DON'T)
C     UNDEF  UNDEFINED VALUE WHICH IF FOUND IN INPUT ARRAY CAUSES
C            THAT LOCATION TO BE IGNORED IN INTERPOLATION.  USED
C            AS THE OUTPUT VALUE FOR OUTPUT POINTS WITH NO DEFINED
C            AND/OR UNMASKED DATA
C     WORK   WORK ARRAY DIMENSIONED NCAT BY LONOUT BY LATOUT
C     NCAT   NUMBER OF CATAGORIES FOUND IN FIELDA
C     WRK2   WORK ARRAY DIMENSIONED AS OUTPUT ARRAY
C
      DO 10 MM=1,7
      MDIST(MM)=0
      NDIST(MM)=0
   10 CONTINUE
      DO 200 J=1,LATOUT
      DO 100 I=1,LONOUT
      FIELDB(I,J)=0.0
      WRK2(I,J)=0.0
  100 CONTINUE
  200 CONTINUE
      DO 500 J=1,LATOUT
      DO 400 I=1,LONOUT
      DO 300 N=1,NCAT
      WORK(N,I,J)=0.0
  300 CONTINUE
  400 CONTINUE
  500 CONTINUE
      DO 1000 J=1,LATD
      WLT=WTLAT(J)
      LTI=MPLAT(J,1)
      LTO=MPLAT(J,2)
      DO 900 I=1,LOND
      LNI=MPLON(I,1)
      IF(MSKIN(LNI,LTI).EQ.0)GO TO 900
      IF(FIELDA(LNI,LTI).EQ.UNDEF)GO TO 900
      WLN=WTLON(I)
      LNO=MPLON(I,2)
      NC=FIELDA(LNI,LTI)
      IF(NC.GT.NCAT.OR.LNO.GT.LONOUT.OR.LTO.GT.LATOUT)GO TO 3000
      IF(NC.LT.1.OR.LNO.LT.1.OR.LTO.LT.1)GO TO 3000
      WORK(NC,LNO,LTO)=WORK(NC,LNO,LTO)+WLT*WLN
      WRK2(LNO,LTO)=WRK2(LNO,LTO)+WLT*WLN
  900 CONTINUE
 1000 CONTINUE
      FQ=1.0
      ND=0
      NS=0
      DO 1200 J=1,LATOUT
      DO 1100 I=1,LONOUT
      FIELDB(I,J)=UNDEF
      IF(WRK2(I,J).EQ.0.0)GO TO 1100
      FM=0.0
      NX=UNDEF
      MM=0
      NN=1
      B(1)=0.0
      B(2)=0.0
      B(3)=0.0
      B(4)=0.0
      B(5)=0.0
      DO 1050 N=1,NCAT
      FR=WORK(N,I,J)/WRK2(I,J)
      IF(FM.LT.FR)THEN
      FM=FR
      NX=N
      END IF
      KL=KLASS(N)
      B(KL)=B(KL)+FR
      IF(FR.GT..5)NN=0
      IF(WORK(N,I,J).NE.0.0)MM=MM+1
 1050 CONTINUE
      CMX=0.0
      KMX=0
      DO 1060 K=1,5
      IF(B(K).GT.CMX)THEN
      CMX=B(K)
      KMX=K
      END IF
 1060 CONTINUE
      IF(KLASS(NX).EQ.KMX)THEN
      FIELDB(I,J)=NX
      ND=ND+1
      IF(FM.NE.0.0.AND.FM.LT.FQ)THEN
      FQ=FM
      IQ=I
      JQ=J
      NQ=NX
      END IF
      ELSE
      FMK=0.0
      DO 1070 N=1,NCAT
      IF(KLASS(N).NE.KMX)GO TO 1070
      FRK=WORK(N,I,J)/WRK2(I,J)
      IF(FMK.LT.FRK)THEN
      FMK=FRK
      NXK=N
      END IF
 1070 CONTINUE
      FIELDB(I,J)=NXK
      NS=NS+1
      WRITE(6,1075)NS,I,J
 1075 FORMAT(' NS=',I3,' I=',I3,' J=',I3)
      IF(FMK.NE.0.0.AND.FM.LT.FQ)THEN
      FQ=FMK
      IQ=I
      JQ=J
      NQ=NXK
      END IF
      END IF
      IF(MM.GT.7.AND.MM.GT.0)MM=7
      MDIST(MM)=MDIST(MM)+1
      NDIST(MM)=NDIST(MM)+NN
 1100 CONTINUE
 1200 CONTINUE
      WRITE(6,2000)FQ,IQ,JQ,NQ,(N,WORK(N,IQ,JQ),N=1,NCAT)
 2000 FORMAT(' MINIMUM QUALIFYING FRACTION=',G16.8,' AT I,J=',2I4,
     *' FOR CATAGORY',I3/
     *'  DISTRIBUTION OF AREAS AT THIS LOCATION BY CATAGORY:'/
     *(4(' N=',I3,G14.6)))
      WRITE(6,2200)(MM,MDIST(MM),NDIST(MM),MM=1,7)
 2200 FORMAT(' ',20X,'DISTRIBUTION OF CATAGORIES:'/
     *' # OF CATAGORIES  # OF CASES  # WITHOUT MAJORITY'/
     *(' ',7X,I2,10X,I5,9X,I5))
      WRITE(6,2400)ND,NS
 2400 FORMAT(' NUMBER OF DIRECTLY COMPUTED POINTS:',I5/
     *              ' NUMBER OF SUBSTITUTED POINTS:',I4)
      RETURN
 3000 WRITE(6,3100)NC,LNO,LTO,I,J,LNI,LTI
 3100 FORMAT(' BAD INDICES AT NC,LNO,LTO,I,J,LNI,LTI=',7I6)
      STOP 3100
      END
