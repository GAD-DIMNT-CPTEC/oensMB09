      PROGRAM SNWINT
C*
      INTEGER MEND1,NEND1,JEND1,IMAX,JMAX
C*
      INCLUDE "reshsno.h"
C*
      INTEGER LONG,LATG
      PARAMETER (LONG=IMAX,LATG=JMAX)
      REAL ALB1(LONG,LATG),ALB2(LONG,LATG),
     *     SNOW(LONG,LATG),ALBED(LONG,LATG)
      REAL*4 SNWGRD(LONG,LATG)
      INTEGER IDATE(4),IMON(12),IDATEC(4)
      INTEGER LSMAP(LONG,LATG)
      CHARACTER*7 MSKFMT
      INTEGER I,MON,MF,MN,IADD,M,J
      REAL F1,F2
C*JPB      CHARACTER*8 DATE
      CHARACTER DATE*10
C*
      LOGICAL PICE
      NAMELIST /SNWNML/ PICE
C*
      DATA IMON/31,28,31,30,31,30,31,31,30,31,30,31/
      DATA PICE/.FALSE./
      DATA MSKFMT/'(   I1)'/
C*
C*    OUTPUT FOR PRINTOUT
      OPEN(66,FORM='FORMATTED',STATUS='UNKNOWN')
C*
      READ(*,SNWNML)
      WRITE(66,SNWNML)
C*
C*    OPEN FILES
C*
C*    SEA LAND MASK - INPUT
      OPEN(14,FORM='FORMATTED',STATUS='UNKNOWN')
C*
C*    ALBEDO - INPUT
      OPEN(15,FORM='UNFORMATTED',STATUS='UNKNOWN')
C*
C*    SNOW - OUTPUT (MODEL INPUT)
      OPEN(16,FORM='UNFORMATTED',STATUS='UNKNOWN')
      OPEN(17,FORM='UNFORMATTED',STATUS='UNKNOWN')
C*
      WRITE(MSKFMT(2:4),'(I3)')LONG
C*
C*    READING LAND SEA MASK
C*
      READ(14,MSKFMT)LSMAP
      CLOSE(14)
C*
C*    GETTING DATE FROM ARGUMENT LABELI
C*
      CALL GETARG(1,DATE)
C*JPB      READ(DATE(1:2),'(I2)')IDATE(4)
C*JPB      READ(DATE(3:4),'(I2)')IDATE(2)
C*JPB      READ(DATE(5:6),'(I2)')IDATE(3)
C*JPB      READ(DATE(7:8),'(I2)')IDATE(1)
C*JPB      IDATE(4)=1900+IDATE(4)
      READ(DATE(1:4),'(I4)')IDATE(4)
      READ(DATE(5:6),'(I2)')IDATE(2)
      READ(DATE(7:8),'(I2)')IDATE(3)
      READ(DATE(9:10),'(I2)')IDATE(1)
C*
C*    LINEAR TIME INTERPOLATION FACTORS F1 AND F2
C*
      MON=IDATE(2)
      IF (MOD(IDATE(4),4) .EQ. 0) IMON(2)=29
      MF=MON-1
      IF (IDATE(3) .GT. IMON(MON)/2) MF=MON
      MN=MF+1
      IF (MF .LT. 1) MF=12
      IF (MN .GT. 12) MN=1
      IADD=IMON(MF)/2
      IF (MF .EQ. MON) IADD=-IADD
      F1=2.0*FLOAT(IDATE(3)+IADD)/FLOAT(IMON(MF)+IMON(MN))
      F2=1.0-F1
      WRITE(66,10)IDATE,MF,MN,F1,F2
   10 FORMAT(/,' SNOW FOR IDATE=',4I5,' MF=',I3,' MN=',I3,
     *       ' F1=',F8.5,' F2=',F8.5,/)
C*
C*    READING ALBEDO
C*
      DO M=1,MF
      READ(15)ALB1
      ENDDO
      IF(MF.EQ.12)REWIND(15)
      READ(15)ALB2
      CLOSE(15)
C*
C*    COMPUTES SNOW BASED ON ALBEDO
C*
      DO J=1,LATG
      DO I=1,LONG
      SNOW(I,J)=0.0
      ALBED(I,J)=F2*ALB1(I,J)+F1*ALB2(I,J)
      IF (LSMAP(I,J) .NE. 0) THEN
      IF (.NOT.PICE) THEN
      IF (ALBED(I,J).GE.0.40 .AND. ALBED(I,J).LT.0.49) THEN
      SNOW(I,J)=5.0
      ENDIF
      IF (ALBED(I,J).GE.0.49 .AND. ALBED(I,J).LT.0.69) THEN
      SNOW(I,J)=10.0
      ENDIF
      IF (ALBED(I,J).GE.0.69 .AND. ALBED(I,J).LT.0.75) THEN
      SNOW(I,J)=20.0
      ENDIF
      ENDIF
      IF (ALBED(I,J) .GE. 0.75) THEN
      SNOW(I,J)=3000.0
      ENDIF
      ENDIF
      ENDDO
      ENDDO
C*
C*    32 BITS OUTPUT
C*
      DO J=1,LATG
      DO I=1,LONG
      SNWGRD(I,J)=SNOW(I,J)
      ENDDO
      ENDDO
C*
C*    WRITES SNOW
C*
      WRITE(16)SNWGRD
      WRITE(17)SNWGRD
C*
      CLOSE(16)
      CLOSE(17)
      CLOSE(66)
C*                              
      STOP
      END
