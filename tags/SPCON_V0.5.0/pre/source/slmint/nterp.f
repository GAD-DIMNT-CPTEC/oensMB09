      SUBROUTINE NTERP(FIELDA,LONIN,LATIN,FIELDB,LONOUT,LATOUT,
     *WTLON,WTLAT,MPLON,MPLAT,LOND,LATD,MSKIN,UNDEF,WORK)
      DIMENSION WTLON(LONIN+LONOUT+2),WTLAT(LATIN+LATOUT+2),
     *MPLON(LONIN+LONOUT+2,2),MPLAT(LATIN+LATOUT+2,2),
     *FIELDA(LONIN,LATIN),FIELDB(LONOUT,LATOUT),
     *MSKIN(LONIN,LATIN),WORK(LONOUT,LATOUT)
C     NEW INTERPOLATION SUBROUTINE.
C     SHOULD ONLY BE CALLED AFTER SUBROUTINE WTERP HAS BEEN CALLED.
C     AFTER INTERPOLATION USER IS RESPONSIBLE FOR OUTPUT MASKING AND
C     POLE INTERPOLATION FOR TYPE 2 (POLE CENTERED) GRIDS.  POLE
C     INTERPOLATION CAN BE DONE WITH SUBROUTINE POLAVG.
C
C     SUBROUTINE ARGUMENTS:
C
C     FIELDA INPUT FIELD TO BE INTERPOLATED
C     LONIN  NUMBER OF LONGITUDE POINTS FOR THE INPUT GRID
C     LATIN  NUMBER OF LATITUDE POINTS FOR THE INPUT GRID
C     FIELDB OUTPUT FIELD RESULTING FROM INTERPOLATION
C     LONOUT NUMBER OF LONGITUDE POINTS FOR THE OUTPUT GRID
C     LATOUT NUMBER OF LATITUDE POINTS FOR THE OUTPUT GRID
C     WTLON  AREA WEIGHTS IN THE LONGITUDINAL DIRECTION
C     WTLAT  AREA WEIGHTS IN THE LATITUDIANL DIRECTION
C     MPLON  LONGITUDE INDEX MAPPING FROM INPUT (,1) TO OUTPUT (,2)
C     MPLAT  LATITUDE INDEX MAPPING FROM INPUT (,1) TO OUTPUT (,2)
C     LOND   TOTAL NUMBER OF LONGITUDE WEIGHTS
C     LATD   TOTAL NUMBER OF LATITUDE WEIGTHS
C     MSKIN  INPUT GRID MASK TO CONFINE INTERPOLATION OF INPUT
C            DATA TO CERTAIN AREAS (1=INTERPOLATE, 0=DON'T)
C     UNDEF  UNDEFINED VALUE WHICH IF FOUND IN INPUT ARRAY CAUSES
C            THAT LOCATION TO BE IGNORED IN INTERPOLATION.  USED
C            AS THE OUTPUT VALUE FOR OUTPUT POINTS WITH NO DEFINED
C            AND/OR UNMASKED DATA
C     WORK   WORK ARRAY DIMENSIONED AS OUTPUT ARRAY USED AS A
C            DENOMINATOR
C
C
      DO 200 J=1,LATOUT
      DO 100 I=1,LONOUT
      FIELDB(I,J)=0.0
      WORK(I,J)=0.0
  100 CONTINUE
  200 CONTINUE
      DO 1000 J=1,LATD
      WLT=WTLAT(J)
      LTI=MPLAT(J,1)
      LTO=MPLAT(J,2)
      DO 900 I=1,LOND
      LNI=MPLON(I,1)
      IF(MSKIN(LNI,LTI).EQ.0)GO TO 900
      IF(FIELDA(LNI,LTI).EQ.UNDEF)GO TO 900
      WLN=WTLON(I)
      LNO=MPLON(I,2)
      FIELDB(LNO,LTO)=FIELDB(LNO,LTO)+FIELDA(LNI,LTI)*WLT*WLN
      WORK(LNO,LTO)=WORK(LNO,LTO)+WLT*WLN
  900 CONTINUE
 1000 CONTINUE
      DO 1200 J=1,LATOUT
      DO 1100 I=1,LONOUT
      IF(WORK(I,J).EQ.0)THEN
      FIELDB(I,J)=UNDEF
      ELSE
      FIELDB(I,J)=FIELDB(I,J)/WORK(I,J)
      END IF
 1100 CONTINUE
 1200 CONTINUE
      RETURN
      END
