      SUBROUTINE CHWZ(NM,N,AR,AI,W,ZR,ZI,B,C,D,IERR)
C*
C*    COMPUTES THE EIGENVALUES AND EIGENVECTORS
C*    OF A COMPLEX HERMITIAN MATRIX
C*
C*    NM       - FIRST DIMENSION OF MATRICES AR, AI, ZR AND ZI, THE
C*               SAME VALUE SET ON DECLARATION AT THE CALLING ROUTINE
C*    N        - THE ORDER OF THE EIGEN-PROBLEM (MUST BE .LE. NM)
C*    AR(NM,N) - INPUT HERMITAN MATRIX, REAL PART (FULL STORAGE)
C*    AI(NM,N) - INPUT HERMITAN MATRIX, IMAGINARY PART (FULL STORAGE)
C*    W(N)     - OUTPUT EIGENVALUES (ORDERED
C*               IN ASCENDENT MAGNITUDE VALUES)
C*    ZR(NM,N) - EIGENVECTORS, REAL PART (EACH COLUMN IS
C*               ASSOCIATED TO THE CORRESPONDING EIGENVALUE)
C*    ZI(NM,N) - EIGENVECTORS, IMAGINARY PART (EACH COLUMN IS
C*               ASSOCIATED TO THE CORRESPONDING EIGENVALUE)
C*    B(N)     - WORKING VECTOR
C*    C(N)     - WORKING VECTOR
C*    D(2,N)   - WORKING VECTOR
C*    IERR     - ERROR INDEX:
C*             = 0                  - NORMAL TERMINATION
C*             = 10*N               - N IS GREATER THAN NM
C*             = OTHER VALUE THAN 0 - THE COMPUTATION DOES NOT
C*              CONVERGE AFTER 50 ITERATIONS, BUT IT COMPUTES
C*              THE EIGENVALUES TILL IERR.
C*
C*    FORMAL ARGUMENTS:
C*
      INTEGER N,NM,IERR
      REAL AR(NM,N),AI(NM,N),W(N),ZR(NM,N),ZI(NM,N),B(N),C(N),D(2,N)
C*
      INTEGER I,J
      REAL ZS
C*
C*    CHECK FOR INPUT ERRORS
C*
      IF (N .GT. NM) THEN
      IERR=10*N
      RETURN
      ENDIF
C*
C*    FIND EIGENVALUES AND EIGENVECTORS
C*
      CALL HTRIDI(NM,N,AR,AI,W,B,C,D)
C*
      DO I=1,N
      DO J=1,N
      ZR(J,I)=0.0
      ZI(J,I)=0.0
      ENDDO
      ZR(I,I)=1.0
      ENDDO
C*
      CALL TQL2(NM,N,W,B,ZR,IERR)
C*
      IF (IERR .EQ. 0) THEN
      CALL HTRIBK(NM,N,AR,AI,D,N,ZR,ZI)
C*
C*    NORMALIZE EIGENVECTORS TO MAGNITUDE ONE
C*
      DO J=1,N
      ZS=0.0
      DO I=1,N
      ZS=ZS+ZR(I,J)*ZR(I,J)+ZI(I,J)*ZI(I,J)
      ENDDO
      ZS=1.0/SQRT(ZS)
      DO I=1,N
      ZR(I,J)=ZS*ZR(I,J)
      ZI(I,J)=ZS*ZI(I,J)
      ENDDO
      ENDDO
      ENDIF
C*
      RETURN
      END
